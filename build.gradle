
plugins {
    id "org.jetbrains.intellij" version "0.4.21"
	id 'de.undercouch.download' version '3.4.3'
}

repositories {
    mavenCentral()
	flatDir {
	    dirs buildDir
	}
    maven { url 'https://jitpack.io' }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'jacoco'
apply plugin: 'java'
compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    if (ideaVersion.substring(3) == "2020.2") {
        plugins 'com.intellij.javafx:1.0.2'
    }
    pluginName 'org.jboss.tools.intellij.analytics'
    updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

//LSP plugin causes this task to fail
buildSearchableOptions.enabled = false

task downloadFile(type: Download) {
    src 'https://github.com/fabric8-analytics/fabric8-analytics-lsp-server/releases/download/v0.4.4/ca-lsp-server.tar'
    dest new File(buildDir, 'server.tgz')
    overwrite false
}

task downloadAndUnzipFile(dependsOn: downloadFile, type: Copy) {
    from tarTree(resources.bzip2(downloadFile.dest))
    into new File(buildDir, 'server/server')
}

prepareSandbox {
    dependsOn downloadAndUnzipFile
}

dependencies {
    compile files(new File(buildDir, 'server')) {
        builtBy 'downloadAndUnzipFile'
    }
    implementation 'com.github.ballerina-platform:lsp4intellij:master-SNAPSHOT'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version